/**
 * implements a simplified version of the chromium dino game.
 * The player controls a 16x16 dino that must jump over cacti to survive.
 * To jump, press space
 */

class DinoGame {

	static DinoGame instance;
 	field Dino dino;
	field int score;
	field int airtime;
	field boolean jump;
	field boolean exit;
	field Array cactarray;
	field int numCact;
	field int cactimer;
	field int cactindex;
	field int nextCact;
	field Cactus currentCact;
	field int cactimer1;
	field int cactimer2;
	field int cactimer3;
	field int cactimer4;
	field int cactimer5;

	/** Constructs a new Dino game */
	constructor DinoGame new() {
		do Screen.clearScreen();
		do Output.moveCursor(0, 58);
		do Output.printString("00000");
		let cactarray = Array.new(5);
		let dino = Dino.new(10, 220);
		let airtime = 0;
		let nextCact = 0;
		let numCact = 0;
		let score = 0;
		let jump = false;
		let exit = false;
		do Screen.drawRectangle(0, 238, 511, 255);
		let cactimer = 30;
		let cactimer1 = 50;
		let cactimer2 = 60;
		let cactimer3 = 70;
		let cactimer4 = 80;
		let cactimer5 = 90;
		return this;
	}

	/** disposes this game */
	method void dispose() {
		do dino.dispose();
		do cactarray.dispose();
		do Memory.deAlloc(this);
		return;
	}

	/** Creates an instance of the Dino game and stores it */
	function void newInstance() {
		let instance = DinoGame.new();
		return;
	}

	/** returns the single instance of this dino game */
	function DinoGame getInstance() {
		return instance;
	}

	/** runs the game: handles the user's inputs and moves the dino accordingly */
	method void run() {
		var char key; // the key currently pressed by the user
		while (~exit) {
			// waits for a key to be pressed
			while ((key = 0) & (~exit)) {
				let key = Keyboard.keyPressed();
				if (jump) {
					do moveDino();
				}
				do updateCacti();
				do updateScore();
				do Sys.wait(30);
			}

			if (key = 81) { let exit = true; }	// q key
			if (key = 32) { let jump = true; }	// space key

			// waits for the key to be released
			while (~(key = 0) & (~exit)) {
				let key = Keyboard.keyPressed();
				if (jump) {
					do moveDino();
				}
				do updateCacti();
				do updateScore();
				do Sys.wait(30);
			}
		}

		if (exit) {
			do Output.moveCursor(10,27);
			do Output.printString("GAME OVER");
		}

		return;
	}

	/** handles all dino movement */
	method void moveDino() {
		if (airtime < 5) { do dino.moveUp(); }
		else { do dino.moveDown(); }
		if (airtime = 9) {
			let airtime = 0;
			let jump = false;
		}
		else { let airtime = airtime + 1; }
		return;
	}

	/** moves, adds, and removes cacti */
	method void updateCacti() {
		/* makes sure there are up to five cacti at a time with a minium of 30
		 * calls of this function in between. whether or not a cactus is actually
		 * created is supposed to be relatively random
		 */
		// TODO: make cacti more random
		if ((cactarray[0] = false) & (cactimer1 = 0)) {
			let nextCact = 0;
			let cactimer = 30;
			let cactimer1 = 50;
		}
		else {
			if ((cactarray[1] = false) & (cactimer2 = 0)) {
				let nextCact = 1;
				let cactimer = 30;
				let cactimer2 = 60;
			}
			else {
				if ((cactarray[2] = false) & (cactimer3 = 0)) {
					let nextCact = 2;
					let cactimer = 30;
					let cactimer3 = 70;
				}
				else {
					if ((cactarray[3] = false) & (cactimer4 = 0)) {
						let nextCact = 3;
						let cactimer = 30;
						let cactimer4 = 80;
					}
					else {
						if ((cactarray[4] = false) & (cactimer5 = 0)) {
							let nextCact = 4;
							let cactimer = 30;
							let cactimer5 = 90;
						}
					}
				}
			}
		}
		if (/*(rand(3)) & */(numCact < 5) & (cactimer = 0)) {
			let cactarray[nextCact] = Cactus.new(1);
			let numCact = numCact + 1;
			let cactimer = 30;
		}
		else { let cactimer = cactimer - 1; }

		let cactindex = numCact - 1;

		while (~(cactindex < 0)) {
			let currentCact = cactarray[cactindex];
			// checks if there is a cactus at the current position
			if (~(currentCact = false)) {
				do currentCact.moveLeft();
				// checks if the cactus is touching the dino
				if (currentCact.checkTouch()) {
					if ((dino.getLocation() = 7073) | (dino.getLocation() = 6817) | (dino.getLocation() = 6561)) {
						let exit = true;
					}
				}
				// checks if the cactus is touching the edge of the screen
				if (currentCact.checkEdge() = true) {
					do currentCact.dispose();
					let currentCact = false;
					let numCact = numCact - 1;
				}
			}
			let cactindex = cactindex - 1;
		}
		return;
	}
	

	function boolean checkCleanDiv(int numerator, int denominator) {
		var int quotient;
		var int product;
		let quotient = numerator / denominator;
		let product = quotient * denominator;
		if (product = numerator) { return true; }
		else { return false; }
	}

	method void updateScore() {
		var int cursor_j;
		var int numlen;
		if (score < 10) { let numlen = 1; } 
		else {
			if (score < 100) { let numlen = 2; } 
			else {
				if (score < 1000) { let numlen = 3; } 
				else {
					if (score < 10000) { let numlen = 4; } 
					else { let score = 0; let numlen = 1; }
				}
			}
		}
		let cursor_j = (64 - numlen);
		do Output.moveCursor(0,cursor_j);
		do Output.printInt(score);
		let score = score + 1;
		return;
	}

	method boolean rand(int num/*, int address*/) {
		var boolean randout;
		var int memcontent;
		var boolean divClean;
		// let memcontent = Memory.peek(address); // arbitrary memory location
		let randout = DinoGame.checkCleanDiv(
		airtime * score + 595 * Math.sqrt(score) * 3, num);
		return randout;
	}
}
