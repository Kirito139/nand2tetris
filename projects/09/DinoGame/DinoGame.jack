/**
 * a simplified version of the chromium dino game.
 * The player controls a 16x16 dino that must jump over cacti to survive.
 * To jump, press space
 */

class DinoGame {

	static DinoGame instance;
	field int score;
	field boolean exit;
	
	// dino
 	field Dino dino;
	field int airtime;
	field boolean jump;

	// cacti
	field Cactus currentCact;
	field Array cactarray;
	field int numCact;
	field int cactimer;
	field int cactindex;
	field int nextCact;

	// random number generator
	static int _lastNum;
	static int PSEUDA_RAND_MULTIPLIER;
	static int PSEUDA_RAND_ADDER;
	static int MAX_DIST;

	/** Constructs a new Dino game */
	constructor DinoGame new() {
		do Screen.clearScreen();
		let exit = false;

		// score
		do Output.moveCursor(0, 58);
		do Output.printString("00000");
		let score = 0;

		// cacti
		let cactarray = Array.new(3);
		let nextCact = 0;
		let numCact = 0;
		let cactimer = 30;

		// dino
		let dino = Dino.new();
		let airtime = 0;
		let jump = false;

		// ground
		do Screen.drawRectangle(0, 238, 511, 255);

		// random number generator
		let _lastNum = 17528;
		let PSEUDA_RAND_MULTIPLIER = 91;
		let PSEUDA_RAND_ADDER = 1;
		let MAX_DIST = 16354;
		return this;
	}

	/** disposes this game */
	method void dispose() {
		do dino.dispose();
		do cactarray.dispose();
		do Memory.deAlloc(this);
		return;
	}

	/** Creates an instance of the Dino game and stores it */
	function void newInstance() {
		let instance = DinoGame.new();
		return;
	}

	/** returns the single instance of this dino game */
	function DinoGame getInstance() {
		return instance;
	}

	/** runs the game: handles the user's inputs and moves the dino accordingly */
	method void run() {
		var char key; // the key currently pressed by the user
		while (~exit) {
			// waits for a key to be pressed
			while ((key = 0) & (~exit)) {
				let key = Keyboard.keyPressed();
				if (jump) {
					do moveDino();
				}
				do updateCacti();
				do updateScore();
				do Sys.wait(30);
			}

			if (key = 81) { let exit = true; }	// ends game if q is pressed
			if (key = 32) { let jump = true; }	// space key triggers a jump

			// waits for the key to be released
			while (~(key = 0) & (~exit)) {
				let key = Keyboard.keyPressed();
				if (jump) {
					do moveDino();
				}
				do updateCacti();
				do updateScore();
				do Sys.wait(30);
			}
		}

		// game end
		if (exit) {
			do Output.moveCursor(10,27);
			do Output.printString("GAME OVER");
		}
		return;
	}

	/** handles all dino movement */
	method void moveDino() {
		if (airtime < 5) { do dino.moveUp(); }
		else { do dino.moveDown(); }
		if (airtime = 9) {
			let airtime = 0;
			let jump = false;
		}
		else { let airtime = airtime + 1; }
		return;
	}

	/** moves, adds, and removes cacti, witha  maximum of 3 cacti at a time */
	method void updateCacti() {
 		var int movecounter;
		let movecounter = 0;
		// first iterates thru the array to see if there are any empty cacti slots
		let cactindex = 2;
		while (~(cactindex < 0)) {
			if (cactarray[cactindex] = false) {
				let nextCact = cactindex;
			}
			let cactindex = cactindex - 1;
		}
		// if there are less than three cacti and the cactimer is 0, creates a new cactus,
		// adds one to numCact to reflect the number of cacti, and resets the cactimer.
		if ((numCact < 5) & (cactimer = 0)) {
			let cactarray[nextCact] = Cactus.new();
			let numCact = numCact + 1;
			let cactimer = DinoGame.random();
		}
		// otherwise, decrements the cactimer.
		else { let cactimer = cactimer - 1; }

		// iterates through the array and moves each cactus,
		// then checks for a collision with the dino or edge of the screen.
		let cactindex = 2;
		while (~(cactindex < 0)) {
			let currentCact = cactarray[cactindex];
			// checks if there is a cactus at the current position
			if (currentCact) {
				do currentCact.moveLeft();
				let movecounter = movecounter + 1;
				// checks if the cactus is touching the dino
				if ((currentCact.checkTouch()) & (dino.getLocation() > 6560)) {
						let exit = true;
				}
				// checks if the cactus is touching the edge of the screen
				if (currentCact.checkEdge() = true) {
					do currentCact.dispose();
					let cactarray[cactindex] = false;
					let numCact = numCact - 1;
				}
			}
			let cactindex = cactindex - 1;
		}
		return;
	}
	

	/** does what you'd expect, updates the score */
	method void updateScore() {
		var int cursor_j;
		var int numlen;
		// compensates for number length so that the score grows left, not right.
		if (score < 10) { let numlen = 1; } 
		else {
			if (score < 100) { let numlen = 2; } 
			else {
				if (score < 1000) { let numlen = 3; } 
				else {
					if (score < 10000) { let numlen = 4; } 
					else { let score = 0; let numlen = 1; }
				}
			}
		}
		let cursor_j = (64 - numlen);
		do Output.moveCursor(0,cursor_j);
		do Output.printInt(score);
		let score = score + 1;
		return;
	}

	/** Generate a pseudo-random number between 10 and 30 */
	function int random() {
		var int res;
		let res = DinoGame.modulo(_lastNum, 20);
		let _lastNum = (_lastNum * PSEUDA_RAND_MULTIPLIER) + PSEUDA_RAND_ADDER;
		let _lastNum = DinoGame.moduloPower2(_lastNum, 1024);
		return res + 10;
	}

	/** Calculates modulo */
	function int modulo(int num, int x) {
		var int res;
		if((~(num > x)) | (x=0)) {
			let res = num;
		} else {
			let res = num - (x * Math.divide(num, x));
		}
		return res;
	}

	/** Provides an efficient way to calculate modulo of 2^n numbers
	 * Assumes x is power of 2 */
	function int moduloPower2(int num, int x) {
		return (num & (x-1));
	}
}
